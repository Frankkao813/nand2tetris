// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.

    // if (zx == 1) sets x = 0        // 16-bit constant
    Mux16(a=x, b=false, sel=zx, out=ozx);

    // if (nx == 1) sets x = !x       // bitwise not
    Not16(in=ozx, out=nozx);
    Mux16(a=ozx, b=nozx, sel=nx, out=onx);

    // if (zy == 1) sets y = 0        // 16-bit constant
    Mux16(a=y, b=false, sel=zy, out=ozy);

    // if (ny == 1) sets y = !y       // bitwise not
    Not16(in=ozy, out=nozy);
    Mux16(a=ozy, b=nozy, sel=ny, out=ony);

    // if (f == 1)  sets out = x + y  // integer 2's complement addition
    // if (f == 0)  sets out = x & y  // bitwise and

    And16(a=onx, b=ony, out=onxAndony);
    Add16(a=onx, b=ony, out=onxAddony);
    Mux16(a=onxAndony, b=onxAddony, sel=f, out=tempOut);

    // if (no == 1) sets out = !out   // bitwise not
    Not16(in=tempOut, out=ntempOut);
    Mux16(a=tempOut, b=ntempOut, sel=no, out=out, out[0..7]=outResLow, out[8..15]=outResHigh, out[15]=firstRes);


    //if (out == 0) zr = 1, else zr = 0
    Or8Way(in=outResLow, out=out0);
    Or8Way(in=outResHigh, out=out1);
    Or(a=out0, b=out1, out=out01);
    Not(in=out01, out=zr);

    // if (out < 0)  ng = 1, else ng = 0
    // the most significant bit is the sign bit, so one can use AND(MSB, true) to deduce ng
    And(a=firstRes, b=true, out=ng);


}